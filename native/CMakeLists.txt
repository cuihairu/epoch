cmake_minimum_required(VERSION 3.20)
project(epoch_native VERSION 0.1.0 LANGUAGES C)

set(AERON_ROOT ${CMAKE_CURRENT_SOURCE_DIR}/../third_party/aeron)
set(AERON_CLIENT_DIR ${AERON_ROOT}/aeron-client/src/main/c)
if (EXISTS ${AERON_CLIENT_DIR}/CMakeLists.txt)
    include(CheckIncludeFile)
    file(STRINGS ${AERON_ROOT}/version.txt AERON_VERSION_TXT LIMIT_COUNT 1)
    string(REGEX MATCH "^([0-9]+)\\.([0-9]+)\\.([0-9]+)" _ ${AERON_VERSION_TXT})
    set(AERON_VERSION_MAJOR ${CMAKE_MATCH_1})
    set(AERON_VERSION_MINOR ${CMAKE_MATCH_2})
    set(AERON_VERSION_PATCH ${CMAKE_MATCH_3})
    execute_process(
        COMMAND git -C ${AERON_ROOT} rev-parse --short HEAD
        OUTPUT_VARIABLE AERON_VERSION_GITSHA
        OUTPUT_STRIP_TRAILING_WHITESPACE
        ERROR_QUIET)
    if (AERON_VERSION_GITSHA STREQUAL "")
        set(AERON_VERSION_GITSHA "unknown")
    endif()
    add_definitions(-DAERON_VERSION_TXT="${AERON_VERSION_TXT}")
    add_definitions(-DAERON_VERSION_MAJOR=${AERON_VERSION_MAJOR})
    add_definitions(-DAERON_VERSION_MINOR=${AERON_VERSION_MINOR})
    add_definitions(-DAERON_VERSION_PATCH=${AERON_VERSION_PATCH})
    add_definitions(-DAERON_VERSION_GITSHA="${AERON_VERSION_GITSHA}")
    set(AERON_INSTALL_TARGETS OFF CACHE BOOL "" FORCE)
    add_subdirectory(${AERON_CLIENT_DIR} ${CMAKE_CURRENT_BINARY_DIR}/aeron-client)
else()
    message(FATAL_ERROR "Aeron submodule not found. Run: git submodule update --init --recursive")
endif()

add_library(epoch_aeron SHARED src/epoch_aeron.c)
target_include_directories(epoch_aeron PUBLIC ${CMAKE_CURRENT_SOURCE_DIR}/include)
target_link_libraries(epoch_aeron PUBLIC aeron::aeron_static)
set_target_properties(epoch_aeron PROPERTIES OUTPUT_NAME "epoch_aeron")
